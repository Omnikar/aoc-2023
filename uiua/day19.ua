+@\0&ru0 0
°{⊙∘}⊜(□⊜□≠@\n.)¬⌕"\n\n".

↻⊢⊚∵⊐(≍"in"↙2).  # Rotate so "in" state is first
⊂{"R{R}""A{A}"}  # Add stable reject and accept states
:¤¤∵⊐(∩□⊃↙↘⊗@{.) # Separate labels and rules
≡(
  ⊜□≠@,.↘¯1↘1°□
  ⍜(°□⊢⇌|⊂"x<∞:") # Give default always true predicate
  □≡(
    ⊃↙(⊗□↘+1)⊗@:.°□ # Look up label indices
    ⊃(⊗:"xmas"⊢|=@>⊡1|⋕↘2)
    [⊙⊙⊙∘] # [x|m|a|s <|> value next]
  )
)
Rules ← # [R A in …]
⊐≡(⊜⋕∊,+@0⇡10)
Parts ←

Update ← ≡(
  ∩⊡⊙(Parts|Rules)
  :⊙:≡⊃↙⊡3°□:¤
  ≡(⊡:⊙°[⊙⊙∘])
  ⊡⊢⊚<∩×,:-1×2:
)⇡⧻.

PartOne ← /+♭▽:Parts⍢Update(/↥>1)↯⧻Parts2

PartOne
